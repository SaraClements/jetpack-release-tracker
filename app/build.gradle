apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    // get the first 8 chars of the latest git commit, and use it as the version build name
    def appBuildPrefix =  ['sh', '-c', 'git rev-parse --short=8 HEAD'].execute().text.trim()

    defaultConfig {
        applicationId "name.lmj0011.jetpackreleasetracker"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 6
        versionName "1.2.1"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // ref: https://stackoverflow.com/a/48674264/2445763
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            resValue("bool", "DEBUG_MODE", "false")
            resValue("string", "app_build", appBuildPrefix)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            resValue("bool", "DEBUG_MODE", "true")
            resValue("string", "app_build", appBuildPrefix)
            minifyEnabled false
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)


    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }

    // Big Up! to cSn: https://stackoverflow.com/a/36626584/2445763
    task generateDependencyListFile {
        configurations.implementation.setCanBeResolved(true)
        configurations.androidTestImplementation.setCanBeResolved(true)

        doLast {
            def str = "# auto-generated from ${this.name}; this file should be checked into version control\n"
            def resolvedImplementationConfig = configurations.implementation.resolvedConfiguration
            def resolvedAndroidTestImplementationConfig = configurations.androidTestImplementation.resolvedConfiguration

            resolvedImplementationConfig.firstLevelModuleDependencies.each { dep ->
                str += "${dep.moduleGroup}:${dep.moduleName}:${dep.moduleVersion}\n"
            }

            resolvedAndroidTestImplementationConfig.firstLevelModuleDependencies.each { dep ->
                str += "${dep.moduleGroup}:${dep.moduleName}:${dep.moduleVersion}\n"
            }

            new File(projectDir, "deps.list.txt").text = str
        }
    }

    preBuild.dependsOn(generateDependencyListFile)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation "androidx.navigation:navigation-fragment:$fragment_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$fragment_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Room dependencies
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // GSON
    implementation 'com.google.code.gson:gson:2.8.6'

    // Lifecycle-aware components
    // ref: https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutine"

    // androidx.preference
    implementation "androidx.preference:preference-ktx:1.1.1"

    // WorkManager
    def work_version = "2.3.4"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // https://developer.android.com/studio/write/java8-support?utm_source=android-studio-4-0&utm_medium=studio-assistant-stable#library-desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'

    // add the Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:17.4.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.1'

    // other 3rd party libs
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.vdurmont:semver4j:3.1.0'
    implementation 'br.com.simplepass:loading-button-android:2.2.0'
    implementation 'com.github.kittinunf.fuel:fuel:2.2.3'
}


